<прогр>::=program <назва> var <спис_огол1> { <спис_опер1> }
<назва>::=IDN
<спис_огол1>::=<спис_огол>
<спис_огол>::=<тип> <спис_ід1>|<спис_огол> ¶ <тип> <спис_ід1>
<спис_ід1>::=<спис_ід>
<спис_ід>::=IDN|<спис_ід> , IDN
<спис_опер1>::=<спис_опер>
<спис_опер>::=<опер> ¶|<спис_опер> <опер> ¶
<тип>::=int|real
<опер>::=<вивід>|<присв>|<ввід>|<цикл>|<ум_перех>|<тернар_оп>
<присв>::=IDN = <вираз1>
<ввід>::=Read ( <спис_ід1> )
<вивід>::=Write ( <спис_ід1> )
<цикл>::=for IDN = <вираз1> by <вираз1> to <вираз1> do <опер> rof
<ум_перех>::=if <лог_вираз1> then <спис_опер1> fi
<тернар_оп>::=IDN = <лог_вираз1> ? <вираз1> : <вираз1>
<лог_вираз1>::=<лог_вираз>
<лог_вираз>::=<лог_терм1>|<лог_вираз> or <лог_терм1>
<лог_терм1>::=<лог_терм>
<лог_терм>::=<лог_мн>|<лог_терм> and <лог_мн>
<лог_мн>::=<відн>|( <лог_вираз> )|not <лог_мн>
<відн>::=<вираз1> <зн_відн> <вираз1>
<зн_відн>::=<|>|==|<=|>=|!=
<вираз1>::=<вираз>
<вираз>::=<терм1>|<вираз> + <терм1>|<вираз> - <терм1>
<терм1>::=<терм>
<терм>::=<мн1>|<терм> * <мн1>|<терм> / <мн1>
<мн1>::=<мн>
<мн>::=IDN|CDN|( <вираз1> )